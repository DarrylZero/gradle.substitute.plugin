buildscript {
    repositories {
        maven { url 'https://clojars.org/repo' }
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M6'
        classpath 'com.steammachine.org:gradle.plugins:0.9.3'
        classpath 'com.steammachine.org:gradle.copy.plugin:0.9.0'
    }
}

plugins {
    id 'groovy'
    id 'java'
    id 'maven-publish'
    id "maven"
    id "org.sonarqube" version "2.5"
}
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'com.steammachine.org.gradle.properties.plugins'
apply plugin: 'com.steammachine.org.gradle.copyresources.plugin'


copyresources.copy('copyGroovy') {
    Copy t ->
        t.from  file('src/test/groovy')
        t.into  file('build/classes/test')
        t.include '**/**resource*', '**/**/build.gradle'
}

sonarqube {
    properties {
        property 'sonar.jacoco.reportPaths', "$buildDir/jacoco/test.exec"
//        property 'sonar.tests', "$buildDir/classes/test"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation localGroovy()
    implementation gradleApi()
    testCompile gradleTestKit()
    testCompile 'junit:junit:4.12'
}
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

auxprops.set('deploy') {
    propertyFile file('publish.properties')
}

group = deploy.group
version = deploy.version

jar {
    manifest {
        attributes(
                "Implementation-Title": deploy.title,
                "Implementation-Version": deploy.version,
                "Description": deploy.description
        )
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    from sourceSets.main.allGroovy
    classifier "sources"
    manifest {
        attributes(
                "Implementation-Title": deploy.sourcecode_description,
                "Implementation-Version": deploy.version,
                "Description": deploy.sourcecode_description
        )
    }
}

def artefacts_publications_user = hasProperty('repo_user') ? repo_password : 'repo_user'
def artefacts_publications_password = hasProperty('repo_password') ? repo_password : 'repo_password'

publishing {
    repositories {
        maven {
            credentials {
                username = artefacts_publications_user
                password = artefacts_publications_password
            }
            url 'https://clojars.org/repo'
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId deploy.group
            artifactId deploy.artifact
            version deploy.version
            from components.java

            artifact sourceJar
        }
    }
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = file('build/classes/main')
        testOutputDir = file('build/classes/test')
    }
}

task buildandsources(dependsOn: [build, sourceJar])

//git tag -a v1.4 -m 'my version 1.4'
task tagCommandLine {
    doLast {
        println "git checkout release$deploy.version"
        println "git tag -a v$deploy.version " + " -m \"version $deploy.version\""
    }
}
